
:: OCR processes

:: Prepare temp and output directory
@mkdir %OCRin%  2> nul
@mkdir %OCRout%  2> nul

for /f "tokens=*" %%a in  ('robocopy "%SourceFolder%\tif" NULL *.tif /S /L /NDL /NC /TEE /NJH /NJS /NODD /NS') DO (
:: Clean BW image for OCR
%magick% ^( "%%a[0]" -auto-orient -colorspace gray -type grayscale -contrast-stretch 0 ^) ^( -clone 0 -colorspace gray -negate -lat 15x15+10%% -contrast-stretch 0 ^) -compose copy_opacity -composite -fill "white" -opaque none -alpha Off -sharpen 0x1 "%OCRin%\%%~na_bw.tif"

:: OCR image - PDF is text only, no image
%tesseract% "%OCRin%\%%~na_bw.tif" "%OCRin%\%%~na" --psm 3 -c textonly_pdf=1 pdf txt alto hocr

:: Make Image PDF for display - set output size/quality here
%magick% "%%a[0]" -auto-orient -resample 200 -unsharp 1.5x1+0.7+0.02 -shave 1 -colorspace sRGB -profile "%sRGB%"-depth 8 -compress JPEG -quality 35 "%OCRin%\%%~na_img.pdf"

:: Merge text and image PDF
%pdftk% "%OCRin%\%%~na.pdf" background "%OCRin%\%%~na_img.pdf" output "%OCRin%\%%~na_merged.pdf"
)
@echo Merge PDF
%pdftk% "%OCRin%\%ItemID%*_merged.pdf" cat output "%OCRin%\%ItemID%_combined.pdf" dont_ask

@echo Embed metadata in merged PDF file from XMP
%exiftool% -tagsFromFile "%SourceFolder%\%~n1.xmp" -overwrite_original "%OCRin%\%ItemID%_combined.pdf" 

@echo Linearise PDF 
%qpdf%  --linearize "%OCRin%\%ItemID%_combined.pdf" "%OCRout%\%ItemID%.pdf"

@echo Make thumbnail of PDF
magick "%OCRout%\%ItemID%.pdf[0]"  -limit thread 2 -background white -alpha remove -strip -resize 512x512^> -unsharp 1.5x1+0.7+0.02 -quality 25%% -depth 8 %TempFolder%\%itemid%.jpg

robocopy "%TempFolder%" "%DestinationFolder%\thumb_pdf" "%ItemID%.jpg" /MOV /W:5

@echo Move non-image outputs into filetype-based subfolders

%exiftool% -directory="%OCRout%\%ItemID%_alto_xml" -overwrite_original "%OCRin%\*.xml" 
%exiftool% -directory="%OCRout%\%ItemID%_txt" -overwrite_original "%OCRin%\*.txt" 
%exiftool% -directory="%OCRout%\%ItemID%_hocr" -overwrite_original "%OCRin%\*.hocr"

@echo Create a composite text file
@del "%DestinationFolder%\%ItemID%_ocr.txt" 2> nul
FOR %%f IN ("%OCRout%\%ItemID%_txt\*.txt") DO type %%f >> "%OCRout%\%ItemID%_ocr.txt"


@echo Cleanup temporary files

:: skip cleanup during testing to allow checking of intermediate files
goto skipcleanup
rmdir /s /q %OCRin%

:skipcleanup

:: Pause to debug screen output
pause
